apply plugin: 'signing'
apply plugin: 'com.netflix.nebula.maven-publish'
apply plugin: 'com.netflix.nebula.publish-verification'

signing {
    // https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials
    sign publishing.publications.nebula
}

tasks.withType(Sign).configureEach {
    onlyIf { project.hasProperty('signing.password') }
}

publishing {
    publications {
        nebula(MavenPublication) { publication ->
            pom {
                packaging = 'jar'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
            }
            sourcesJar
            javadocJar
        }
    }

    repositories {
        maven {
            // creates a task `publishNebulaPublicationToStagingDeployRepository`
            it.name = 'StagingDeploy'
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}
